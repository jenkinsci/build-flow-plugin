plugins {
    id 'org.jenkins-ci.jpi' version '0.18.0'
    id 'groovy'
}

version = '0.7-SNAPSHOT'
description = 'Allows multiple SCM plugins to be used in a job.'
group = 'org.jenkins-ci.plugins'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

// Remove all directories from sourceSets.main.java.srcDirs
// so that everything compiles with the Groovy compiler.
// Some Java files inherit from classes in Groovy files.
sourceSets.main.java.srcDirs = [ "" ]
sourceSets.main.groovy.srcDirs += ["src/main/java"]

// Only compile properties with with Java compiler.
// This is for Messages.properties
sourceSets.main.java.srcDirs = [ "src/main/resources/*.properties" ]

// Make sure that generated Java files get compiled by the Java compiler
// This is for Messages.java
sourceSets.main.java.srcDirs += [ "build/generated-src/localizer" ]

//license {
 //   header = file('LICENSE')
//}


jenkinsPlugin {
    // version of Jenkins core this plugin depends on, must be 1.420 or later
    coreVersion = '1.609.1'
    shortName = 'build-flow-plugin'
    displayName = 'Build Flow plugin'
    url = 'https://wiki.jenkins-ci.org/display/JENKINS/Build+Flow+Plugin'
    gitHubUrl = 'https://github.com/jenkinsci/build-flow-plugin.git'

    // use the plugin class loader before the core class loader
    pluginFirstClassLoader = true

    // the developers section is optional, and corresponds to the POM developers section
    developers {
        developer {
            id 'rodrigc'
            name 'Craig Rodrigues'
            email 'rodrigc@FreeBSD.org'
        }
    }
}

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    }
    tasks.withType(GroovyCompile) {
        options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    }
}

dependencies {
    compile "org.jgrapht:jgrapht-jdk1.5:0.7.3"
    compile 'com.cloudbees:groovy-cps:1.7@jar'
    jenkinsPlugins 'org.jenkins-ci.plugins:matrix-project:1.7@jar'
    jenkinsPlugins 'org.jenkins-ci.plugins:junit:1.13@jar'

    // Test dependencies
    jenkinsTest "org.jenkins-ci.plugins:ant:1.3@jar"
    jenkinsTest "org.mockito:mockito-all:1.8.5@jar"
    jenkinsTest 'org.jenkins-ci.plugins:script-security:1.19@jar'
    jenkinsTest 'org.jenkins-ci.main:maven-plugin:2.12.1@jar'
    jenkinsTest 'org.jenkins-ci.plugins:javadoc:1.3@jar'
    jenkinsTest 'org.jenkins-ci.plugins:mailer:1.17@jar'

    // Test dependencies for pipeline plugin
    jenkinsTest "org.jenkins-ci.plugins:durable-task:1.10@jar"
    jenkinsTest "org.jenkins-ci.plugins.workflow:workflow-api:1.13@jar"
    jenkinsTest "org.jenkins-ci.plugins.workflow:workflow-basic-steps:1.13@jar"
    jenkinsTest "org.jenkins-ci.plugins.workflow:workflow-cps:1.13@jar"
    jenkinsTest "org.jenkins-ci.plugins.workflow:workflow-durable-task-step:1.13@jar"
    jenkinsTest "org.jenkins-ci.plugins.workflow:workflow-job:1.13@jar"
    jenkinsTest "org.jenkins-ci.plugins.workflow:workflow-scm-step:1.13@jar"
    jenkinsTest "org.jenkins-ci.plugins.workflow:workflow-step-api:1.13@jar"
    jenkinsTest "org.jenkins-ci.plugins.workflow:workflow-support:1.13@jar"
}
